apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.chrynan.android_search"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/common.kotlin_module'
    }
    // Note: compileOptions is only required for minSdkVersion < 24
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "abi"

    productFlavors {
        x86 { dimension "abi" }
        x86_64 { dimension "abi" }
        arm { dimension "abi" }
        aarch64 { dimension "abi" }
    }
}

// Allows use of Experimental Kotlin features, such as, Kotlin Contracts
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ['-XXLanguage:+InlineClasses']
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.11"

    // Kotlin Android Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'

    // Coroutine Utilities
    implementation 'com.github.chRyNaN.android-coroutine-utils:core:v0.1.4'
    implementation 'com.github.chRyNaN.android-coroutine-utils:lifecycle:v0.1.4'

    // Dependency Injection
    kapt "com.google.dagger:dagger-compiler:2.19"
    implementation "com.google.dagger:dagger:2.19"
    implementation "com.google.dagger:dagger-android-support:2.19"

    // Moshi - JSON Parsing
    implementation 'com.squareup.moshi:moshi:1.8.0'

    // Retrofit - Rest Library
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'

    // Android
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'

    // Android Adapter
    implementation 'com.github.chRyNaN.aaaah:library:v0.2.1'
    implementation 'com.github.chRyNaN.aaaah:annotation:v0.2.1'
    kapt 'com.github.chRyNaN.aaaah:compiler:v0.2.1'

    // Mapper
    implementation 'com.github.chRyNaN.mapper:mapper-jvm:v1.0.2'

    // Glide - Image Loading
    implementation('com.github.bumptech.glide:glide:4.8.0') {
        exclude group: "com.android.support"
    }
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    // Glide - Image Loading Utilities
    implementation 'com.github.chRyNaN.glide-drawable:library:v0.1.3'
    implementation 'com.github.chRyNaN.glide-drawable:feature:v0.1.3'

    // Kotlin Utilities
    implementation 'com.github.chRyNaN.kotlin-utils:platform-jvm:v0.1.1'

    // Android View Utils
    implementation 'com.github.chRyNaN:android-view-utils:v0.1.0'
    //implementation 'com.github.chRyNaN.inline-pixel:android:v0.1.6'

    // Android Views DSL
    implementation 'com.github.chRyNaN:android-views:v0.1.11'
    implementation 'com.github.chRyNaN.android-views:constraintlayout:v0.1.11'
    implementation 'com.github.chRyNaN.android-views:design:v0.1.11'

    // Stack
    implementation 'com.github.chRyNaN.queue:queue-jvm:v0.1.2'

    // Web - Mozilla GeckoView
    implementation "org.mozilla.components:browser-session:0.35.1"
    implementation "org.mozilla.components:concept-engine:0.35.1"
    implementation "org.mozilla.components:browser-engine-gecko-nightly:0.35.1"
    implementation "org.mozilla.components:browser-engine-system:0.35.1"
    x86Implementation "org.mozilla.geckoview:geckoview-${geckoviewChannel}-x86:${geckoviewVersion}"
    x86_64Implementation "org.mozilla.geckoview:geckoview-${geckoviewChannel}-x86_64:${geckoviewVersion}"
    armImplementation "org.mozilla.geckoview:geckoview-${geckoviewChannel}-armeabi-v7a:${geckoviewVersion}"
    aarch64Implementation "org.mozilla.geckoview:geckoview-${geckoviewChannel}-arm64-v8a:${geckoviewVersion}"

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}